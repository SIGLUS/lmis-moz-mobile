apply plugin: 'jacoco'

def buildDir = "$project.buildDir"

jacoco {
    toolVersion "0.8.6"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testLocalDebugUnitTest") {
    group = "Reporting"
    reports {
        xml.enabled = true
        html.enabled = true
        html.destination file("$buildDir/jacoco")
        csv.enabled false
        xml.destination file("../build/reports/jacocoTestReport.xml")
    }

    def coverageSourceDirs = ["./src/main/java"]
    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/*$ViewInjector*.*',
            '**/*$ViewBinder*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            'android/**',
            'com/google/**',
            'com/intellij/**',
            'junit/**',
            'net/**',
            'okhttp/**',
            'org/**',
            'rx/**',
            '**/*_MembersInjector.class',
            '**/Dagger*Component.class', // covers component implementations
            '**/Dagger*Component$Builder.class', // covers component builders
            '**/*Module_*Factory.class'
    ]
    def javaClasses = fileTree(
            dir: "$buildDir/intermediates/javac/localDebug/classes/org",
            excludes: fileFilter
    )
    classDirectories.from = files([javaClasses])
    sourceDirectories.from = files([coverageSourceDirs])
    additionalSourceDirs.from = files([coverageSourceDirs])
    executionData.from = fileTree(dir: "$buildDir", includes: [
            "jacoco/testLocalDebugUnitTest.exec"
    ])
}